---
title: 6.4.10 同步指令
categories: 
  - 7 深入理解Java虛拟机：JVM高级特性与最佳实践(第3版)
  - 3第三部分 虚拟机执行子系统
  - 第6章 类文件结构
  - 6.4 字节码指令简介
abbrlink: 2c1889b
date: 2021-11-20 21:33:30
updated: 2022-04-03 01:21:18
---
# 6.4.10 同步指令
Java虚拟机可以支持方法级的同步和方法内部一段指令序列的同步，这两种同步结构都是使用管程（Monitor，更常见的是直接将它称为“锁”）来实现的。

方法级的同步是隐式的，无须通过字节码指令来控制，它实现在方法调用和返回操作之中。虚拟机可以从方法常量池中的方法表结构中的ACC_SYNCHRONIZED访问标志得知一个方法是否被声明为同步方法。当方法调用时，调用指令将会检查方法的ACC_SYNCHRONIZED访问标志是否被设置，如果设置了，执行线程就要求先成功持有管程，然后才能执行方法，最后当方法完成（无论是正常完成还是非正常完成）时释放管程。在方法执行期间，执行线程持有了管程，其他任何线程都无法再获取到同一个管程。如果一个同步方法执行期间抛出了异常，并且在方法内部无法处理此异常，那这个同步方法所持有的管程将在异常抛到同步方法边界之外时自动释放。

同步一段指令集序列通常是由Java语言中的synchronized语句块来表示的，Java虚拟机的指令集中有monitorenter和monitorexit两条指令来支持synchronized关键字的语义，正确实现synchronized关键字需要Javac编译器与Java虚拟机两者共同协作支持，譬如有代码清单6-6所示的代码。

<center>代码清单6-6 代码同步演示</center>

```java
void onlyMe(Foo f) {
    synchronized(f) {
        doSomething();
    }
}
```
编译后，这段代码生成的字节码序列如下：
```
Method void onlyMe(Foo) 
0   aload_1             // 将对象f入栈 
1   dup                 // 复制栈顶元素（即f的引用） 
2   astore_2            // 将栈顶元素存储到局部变量表变量槽 2中 
3   monitorenter        // 以栈定元素（即f）作为锁，开始同步 
4   aload_0             // 将局部变量槽 0（即this指针）的元素入栈 
5   invokevirtual #5    // 调用doSomething()方法 
8   aload_2             // 将局部变量Slow 2的元素（即f）入栈 
9   monitorexit         // 退出同步 
10  goto 18             // 方法正常结束，跳转到18返回 
13  astore_3            // 从这步开始是异常路径，见下面异常表的Taget 13 
14  aload_2             // 将局部变量Slow 2的元素（即f）入栈 
15  monitorexit         // 退出同步 
16  aload_3             // 将局部变量Slow 3的元素（即异常对象）入栈 
17  athrow              // 把异常对象重新抛出给onlyMe()方法的调用者 
18  return              // 方法正常返回 

Exception table: 
FromTo  Target  Type 
4       10      13 any 
13      16      13 any
```
编译器必须确保无论方法通过何种方式完成，方法中调用过的每条monitorenter指令都必须有其对应的monitorexit指令，而无论这个方法是正常结束还是异常结束。

从代码清单6-6的字节码序列中可以看到，为了保证在方法异常完成时monitorenter和monitorexit指令依然可以正确配对执行，编译器会自动产生一个异常处理程序，这个异常处理程序声明可处理所有的异常，它的目的就是用来执行monitorexit指令。
